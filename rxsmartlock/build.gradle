apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

gradle.ext.isCiServer = System.getenv().containsKey("CI")
logger.warn("Running on CI: ${gradle.ext.isCiServer}")

if (gradle.ext.isCiServer) {
    apply plugin: "com.vanniktech.maven.publish"
    mavenPublish {
        targets {
            uploadArchives {
                releaseRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        // disabling this lint check because of Timber
        // https://github.com/JakeWharton/timber/issues/242
        disable 'ObsoleteLintCustomCheck'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.media:media:1.1.0'

    implementation 'javax.inject:javax.inject:1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.google.android.gms:play-services-gcm:16.1.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.16'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
